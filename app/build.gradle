apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.google.gms.google-services'

// query git for the SHA, Tag and commit count. Use these to automate versioning.
def gitSha = 'git rev-parse --short HEAD'.execute([], project.rootDir).text.trim()
def gitTag = 'git describe --tags'.execute([], project.rootDir).text.trim()
def gitCommitCount = 100 +
        Integer.parseInt('git rev-list --count HEAD'.execute([], project.rootDir).text.trim())

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"

    defaultConfig {
        applicationId "br.net.paulofernando.moviest"
        minSdkVersion 21
        targetSdkVersion 23
        versionCode gitCommitCount
        versionName "1.0.0"
        archivesBaseName = "moviest-${versionName}"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            versionNameSuffix '-DEBUG'
        }
        buildTypes.each {
            it.buildConfigField 'String', 'TMDB_KEY', TMDBKey
            it.buildConfigField 'String', 'YOUTUBE_KEY', YoutubeKey
        }
    }

    dataBinding {
        enabled = true
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:25.0.0'
    compile 'com.android.support:design:25.0.0'
    compile 'com.android.support:recyclerview-v7:25.0.0'
    compile 'com.android.support:cardview-v7:25.0.0'
    compile 'com.android.support:palette-v7:25.0.0'
    compile 'com.google.android.gms:play-services-analytics:9.0.0'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.1.0'
    compile 'com.google.code.gson:gson:2.7'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.wang.avi:library:2.1.2'
    compile 'com.anupcowkur:reservoir:3.1.0'
    compile 'cn.pedant.sweetalert:library:1.3'
    compile 'com.nineoldandroids:library:2.4.0'
    compile 'com.daimajia.slider:library:1.1.5@aar'
    compile 'com.github.chrisbanes:PhotoView:1.3.1'

    compile 'io.reactivex:rxjava:1.1.0'
    compile 'io.reactivex:rxjava-async-util:0.21.0'
    compile 'io.reactivex:rxandroid:1.1.0'

    apt 'com.google.dagger:dagger-compiler:2.5'
    compile 'com.google.dagger:dagger:2.5'
    provided 'javax.annotation:jsr250-api:1.0'

    testCompile 'org.assertj:assertj-core:2.5.0'
    compile 'com.jakewharton:butterknife:8.2.1'
    apt 'com.jakewharton:butterknife-compiler:8.2.1'
    compile files('libs/YouTubeAndroidPlayerApi.jar')
    compile project(':bypass')
}
